import PrepareExerciseIndexTask

/*
 * Splitfit
 * Copyright (C) 2020  Noah Jutz
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id("com.android.application")
    id("kotlin-android")
    kotlin("plugin.serialization") version "1.9.0"
    id("org.jlleitschuh.gradle.ktlint") version "11.5.0"
    id("com.google.devtools.ksp")
}

android {
    compileSdk = 33
    buildToolsVersion = "34.0.0"

    defaultConfig {
        applicationId = "com.boekenbox.gymroutines"
        minSdk = 21
        targetSdk = 33
        versionCode = 47
        versionName = "0.1.0-beta15"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        sourceSets {
            getByName("androidTest").assets.srcDirs("$projectDir/schemas")
            getByName("main").assets.srcDirs(
                "$projectDir/src/main/assets",
                "$buildDir/intermediates/exerciseIndex",
                "$projectDir/src/main/exercisedb-api-main"
            )
        }
    }

    lint {
        textReport = true
    }

    buildTypes {
        getByName("release") {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            )
        }
        debug {
            applicationIdSuffix = ".dev"
            versionNameSuffix = "-dev"
        }
    }

    buildFeatures {
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.1"
    }

    packaging {
        resources.excludes.addAll(
            listOf(
                "META-INF/DEPENDENCIES",
                "META-INF/LICENSE",
                "META-INF/LICENSE.txt",
                "META-INF/LICENSE.md",
                "META-INF/LICENSE-notice.md",
                "META-INF/license.txt",
                "META-INF/NOTICE",
                "META-INF/NOTICE.txt",
                "META-INF/notice.txt",
                "META-INF/ASL2.0",
                "META-INF/AL2.0",
                "META-INF/LGPL2.1",
                "META-INF/*.kotlin_module"
            )
        )
    }
    namespace = "com.noahjutz.gymroutines"
}

dependencies {
    implementation("androidx.core:core-ktx:1.10.1")
    implementation("androidx.core:core-splashscreen:1.0.1")

    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")

    implementation("com.google.android.material:material:1.9.0")

    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0-RC")

    implementation("androidx.room:room-ktx:2.5.2")
    ksp("androidx.room:room-compiler:2.5.2")
    implementation("androidx.room:room-runtime:2.5.2")
    testImplementation("androidx.room:room-testing:2.5.2")
    androidTestImplementation("androidx.room:room-testing:2.5.2")

    implementation("androidx.lifecycle:lifecycle-common-java8:2.6.1")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1")
    implementation("androidx.lifecycle:lifecycle-process:2.6.1")

    implementation("androidx.navigation:navigation-compose:2.6.0")

    implementation("androidx.activity:activity-compose:1.7.2")

    implementation("androidx.compose.ui:ui:1.4.3")
    implementation("androidx.compose.ui:ui-tooling:1.4.3")
    implementation("androidx.compose.foundation:foundation:1.4.3")
    implementation("androidx.compose.material:material:1.4.3")
    implementation("androidx.compose.material3:material3:1.1.1")
    implementation("androidx.compose.material:material-icons-core:1.4.3")
    implementation("androidx.compose.material:material-icons-extended:1.4.3")
    implementation("androidx.compose.runtime:runtime-livedata:1.4.3")
    androidTestImplementation("androidx.compose.ui:ui-test:1.4.3")
    androidTestImplementation("androidx.compose.ui:ui-test-junit4:1.4.3")

    implementation("io.insert-koin:koin-android:3.3.3")
    implementation("io.insert-koin:koin-androidx-compose:3.4.1")
    testImplementation("io.insert-koin:koin-test:3.3.3")

    implementation("androidx.datastore:datastore-preferences:1.0.0")

    implementation("com.jakewharton:process-phoenix:2.1.2")

    implementation("com.google.accompanist:accompanist-navigation-material:0.30.1")
    implementation("com.google.accompanist:accompanist-navigation-animation:0.30.1")
    implementation("com.google.accompanist:accompanist-placeholder-material:0.30.1")
    implementation("io.coil-kt:coil-compose:2.4.0")

    androidTestImplementation("androidx.test:core:1.5.0")
    androidTestImplementation("androidx.test:core-ktx:1.5.0")

    testImplementation("io.mockk:mockk:1.13.4")
    androidTestImplementation("io.mockk:mockk-android:1.13.4")

    testImplementation("junit:junit:4.13.2")
    testImplementation("org.assertj:assertj-core:3.24.2")
}

ksp {
    arg(RoomSchemaArgProvider(File(projectDir, "schemas")))
}

val exerciseSourceDir = layout.projectDirectory.dir("src/main/exercisedb-api-main")
val exerciseIndexOutput = layout.buildDirectory.dir("intermediates/exerciseIndex")

val prepareExerciseIndex = tasks.register<PrepareExerciseIndexTask>("prepareExerciseIndex") {
    group = "gymroutines"
    description = "Prepare offline exercise index for the built-in exercise catalog"
    sourceDir.set(exerciseSourceDir)
    outputDir.set(exerciseIndexOutput)
}

tasks.named("preBuild").configure {
    dependsOn(prepareExerciseIndex)
}

tasks.configureEach {
    if (name.startsWith("merge") && name.endsWith("Assets")) {
        dependsOn(prepareExerciseIndex)
    }
}

tasks.matching { it.name == "assembleDebug" }.configureEach {
    dependsOn(prepareExerciseIndex)
}
class RoomSchemaArgProvider(
    @get:InputDirectory @get:PathSensitive(PathSensitivity.RELATIVE) val schemaDir: File
) : CommandLineArgumentProvider {
    override fun asArguments(): Iterable<String> {
        return listOf("room.schemaLocation=${schemaDir.path}")
    }
}

kotlin {
    jvmToolchain(17)
}

ktlint {
    android.set(true)
    ignoreFailures.set(true)
}
